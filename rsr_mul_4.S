
/*
 * rsr_mul_4.s
 *
 * Created: 2023-05-21 오후 3:13:06
 *  Author: mingi
 */ 
#include "common.i"
.global rsr_mul_4
.type rsr_mul_4, @function

#define L0 R19
#define L1 R18
#define L2 R17
#define L3 R16

// reduction 4
rsr_mul_4:
	PUSH_REGS

	MOVW R28, R24	// Y = R
	MOVW R30, R22	// Z = L
	MOVW R26, R18	// X = M

	LDD R2, Y+36
	LDD R3, Y+37
	LDD R4, Y+38
	LDD R5, Y+39
	LDD R6, Y+40
	LDD R7, Y+41
	LDD R8, Y+42
	LDD R9, Y+43
	LDD R10, Y+44
	LDD R11, Y+45
	LDD R12, Y+46
	LDD R13, Y+47
	CLR R14	// CARRY 를 위한 레지스터 0으로 세팅
	CLR R15	// BURROW 를 위한 레지스터 0으로 세팅
	CLC		// CARRY FLAG 0으로 세팅

	LDD L0, Z+0
	LDD L1, Z+1
	LDD L2, Z+2
	LDD L3, Z+3

	LDD R0, Z+8		// R8,...,R19,CARRY1
	ADD R2, R0
	LDD R0, Z+9
	ADC R3, R0
	LDD R0, Z+10
	ADC R4, R0
	LDD R0, Z+11
	ADC R5, R0
	LDD R0, Z+12
	ADC R6, R0
	LDD R0, Z+13
	ADC R7, R0
	LDD R0, Z+14
	ADC R8, R0
	LDD R0, Z+15
	ADC R9, R0
	LDD R0, Z+16
	ADC R10, R0
	LDD R0, Z+17
	ADC R11, R0
	LDD R0, Z+18
	ADC R12, R0
	LDD R0, Z+19
	ADC R13, R0
	ADC R14, R1

	LDD R0, Z+4		// R4,...,R15,CARRY2
	ADD R2, R0
	LDD R0, Z+5
	ADC R3, R0
	LDD R0, Z+6
	ADC R4, R0
	LDD R0, Z+7
	ADC R5, R0
	LDD R0, Z+8
	ADC R6, R0
	LDD R0, Z+9
	ADC R7, R0
	LDD R0, Z+10
	ADC R8, R0
	LDD R0, Z+11
	ADC R9, R0
	LDD R0, Z+12
	ADC R10, R0
	LDD R0, Z+13
	ADC R11, R0
	LDD R0, Z+14
	ADC R12, R0
	LDD R0, Z+15
	ADC R13, R0
	ADC R14, R1

	SUB R6, L0		// 0,0,0,0,L0,...,L7,BURROW1
	SBC R7, L1
	SBC R8, L2
	SBC R9, L3
	LDD R0, Z+4
	SBC R10, R0
	LDD R0, Z+5
	SBC R11, R0
	LDD R0, Z+6
	SBC R12, R0
	LDD R0, Z+7
	SBC R13, R0
	ADC R15, R1

	SUB R6, L0		// 0,0,0,0,L0,...,L7,BURROW2
	SBC R7, L1
	SBC R8, L2
	SBC R9, L3
	LDD R0, Z+4
	SBC R10, R0
	LDD R0, Z+5
	SBC R11, R0
	LDD R0, Z+6
	SBC R12, R0
	LDD R0, Z+7
	SBC R13, R0
	ADC R15, R1

	SUB R10, L0		// 0,...,0,L0,L1,L2,L3,BURROW3
	SBC R11, L1
	SBC R12, L2
	SBC R13, L3
	ADC R15, R1

	SUB R10, L0		// 0,...,0,L0,L1,L2,L3,BURROW4
	SBC R11, L1
	SBC R12, L2
	SBC R13, L3
	ADC R15, R1

	SUB R10, L0		// 0,...,0,L0,L1,L2,L3,BURROW5
	SBC R11, L1
	SBC R12, L2
	SBC R13, L3
	ADC R15, R1

	SUB R10, L0		// 0,...,0,L0,L1,L2,L3,BURROW6
	SBC R11, L1
	SBC R12, L2
	SBC R13, L3
	ADC R15, R1

	MOVW R30, R20	// Z = H, H44,H45,H46,H47,0,...,0,CARRY3
	LDD R0, Z+44
	ADD R2, R0
	LDD R0, Z+45
	ADC R3, R0
	LDD R0, Z+46
	ADC R4, R0
	LDD R0, Z+47
	ADC R5, R0
	ADC R6, R1
	ADC R7, R1
	ADC R8, R1
	ADC R9, R1
	ADC R10, R1
	ADC R11, R1
	ADC R12, R1
	ADC R13, R1
	ADC R14, R1

	MOVW R30, R26
	LDD R0, Z+36
	SUB R2, R0
	LDD R0, Z+37
	SBC R3, R0
	LDD R0, Z+38
	SBC R4, R0
	LDD R0, Z+39
	SBC R5, R0
	LDD R0, Z+40
	SBC R6, R0
	LDD R0, Z+41
	SBC R7, R0
	LDD R0, Z+42
	SBC R8, R0
	LDD R0, Z+43
	SBC R9, R0
	LDD R0, Z+44
	SBC R10, R0
	LDD R0, Z+45
	SBC R11, R0
	LDD R0, Z+46
	SBC R12, R0
	LDD R0, Z+47
	SBC R13, R0

	LDD R0, Y+52	// R12~R23 CARRY 처리
	ADD R2, R0
	ADC R3, R1
	ADC R4, R1
	ADC R5, R1
	ADC R6, R1
	ADC R7, R1
	ADC R8, R1
	ADC R9, R1
	ADC R10, R1
	ADC R11, R1
	ADC R12, R1
	ADC R13, R1
	ADC R14, R1

	LDD R0, Y+53	// R12~R23 BURROW 처리
	SUB R2, R0
	SBC R3, R1
	SBC R4, R1
	SBC R5, R1
	SBC R6, R1
	SBC R7, R1
	SBC R8, R1
	SBC R9, R1
	SBC R10, R1
	SBC R11, R1
	SBC R12, R1
	SBC R13, R1
	ADC R15, R1

	// R14, R15에 있는 CARRY/BURROW 처리해주기
	STD Y+36, R2
	STD Y+37, R3
	STD Y+38, R4
	STD Y+39, R5
	STD Y+40, R6
	STD Y+41, R7
	STD Y+42, R8
	STD Y+43, R9
	STD Y+44, R10
	STD Y+45, R11
	STD Y+46, R12
	STD Y+47, R13
	LDD R0, Y+54	// R[52]에 있는 CARRY 가져오기
	ADD R14, R0
	STD Y+54, R14	// CARRY 저장
	LDD R0, Y+55	// R[53]에 있는 BURROW 가져오기
	ADD R15, R0
	STD Y+55, R15	// BURROW 저장

	POP_REGS
RET